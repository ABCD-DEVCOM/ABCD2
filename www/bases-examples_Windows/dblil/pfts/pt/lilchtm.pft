'<br>'

    '<table width="100%">',
	,'<tr>',
	,'<td width="16%">&nbsp;</td>',
           '<td align=justify><font size=2 face=arial>',
/* inicio do formato generico */
,'<p><b>Tipo liter.</b>: ',v5,', <b>Nivel bibl.</b>: ',v6,' - <b>ID</b>: ',v2,'-',mfn,'</p>'/,
/* ,('<p>',v10,'</p>'/),
,('<p>',v16,'</p>'/),
,('<p>',v23,'</p>'/),  */
,'<p>',
/* para documentos textuais, inclusive partituras */
,if a(v9) or v9='a' or v9='c' then
	/* monografia ou serie monografica */
	,if v5*0.1='M' or v5*0.2='MS' or v5*0.1='N' then
		/* nivel analitico */
		,if v6*0.1='a' then
			/* autor pessoal nivel analitico */
			,if p(v10) then
				,(|; |+v10^*,| (|v10^r|)|,if nocc(v10)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v10) then ,'.', fi),
			,fi,
			/* autor institucional nivel analitico */
			,if p(v11) then
				,(|; |+v11^*,if nocc(v11)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v11) then ,'.', fi),
			,fi,
			/* titulo nivel analitico */
			,if p(v12) then
				,' ',(v12^*+| / |),". In: "d12,
			,fi,
		,fi,
		/* nivel monografico */
		,if v6*0.1='m' or v6*0.2='am' then
			/* autor pessoal nivel monografico */
			,if p(v16) then
				,(|; |+v16^*,| (|v16^r|)|,if nocc(v16)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v16) then ,'.', fi),
			,fi,
			/* autor institucional nivel monografico */
			,if p(v17) then
				,(|; |+v17^*,if nocc(v17)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v17) then ,'.', fi),
			,fi,
			/* titulo nivel monografico */
			,if p(v18) then
				," <b>"d18,(v18^*+| / |),"</b>."d18,
				,if v5:'C' then ' In: ' fi,
			,fi,
			/* dados de evento */
			,if v5:'C' then
				/* titulo do evento */
				,if p(v53) then
					,v53,
				,fi,
				/* data normalizada */
				,if p(v55) then
					,|, |v55*0.4,
				,fi,
				/* cidade */
				,if p(v56) then
					,|, |v56,
				,fi,
				/* pais */
				/* ,if p(v57) then
					,|, |v57,
				,fi, */
				/* instituicao patrocinadora */
				,if p(v52) then
					,|, |v57,
				,fi,
				,'.',
			,fi,
			/* edicao */
			,if p(v63) then
				,| |v63|.|,
			,fi,
			/* localizacao - cidade, pais */
			,if p(v66) and v66<>'s.l' then
				,if p(v67) then
					,| |v66,|, |v67|:|,
				,else
					,| |v66|:|,
				,fi,
			,fi,
			/* editora */
			,if p(v62) and v62<>'s.n' then
				,if v9<>'c' then
					,' ',(v62+|; |),
				,else
					,' ',(v62^f|; |),
				,fi,
				,if p(v64) then ,',', else ,'.', fi,
			,fi,
			/* data de publicacao */
			,if a(v64) then
				,' s.d.',
			,else
				,| |v64,'.',
			,fi,
			/* indicacao da quantidade do suporte para partitura */
			/* ex: 1 partitura */
			/* desabilitado */
			/* ,if v9='c' and p(v???) then
				,v???,
			,fi, */
			/* nr. paginas */
			/* somente se nao for analitica */
			,if p(v20) and v6*0.1<>'a' then
				,| |v20,
				,if not v20:'v' and not v20:'p' then
					,' p.',
				,fi,
			,fi,
			/* nr. paginas inicial e final */
			,if p(v14) then
				,| p. |v14^f,|-|v14^l|.|,
			,fi,
			/* informacao descritiva */
			/* se for no formato novo */
			,(if s(v38^a,v38^b,v38^c)<>'' then
				,| |v38^a,
				,if p(v38^b) and p(v38^a) then ,',', fi,
				,| |v38^b,
				,if p(v38^c) and s(v38^a,v38^b)<>'' then ,',', fi,
				,| |v38^c,
				,'.',
			,fi),
			/* se for no formato antigo */
			,if s(v38^a,v38^b,v38^c)='' and s(v38^*)<>'' then
				,' ',(v38+|, |),'.',
			,fi,
			/* nota de conteudo formatada - somente partitura e registro sonoro */
			,if v9='c' and p(v505) then
				,' ',(v505+|. |),
			,fi,
			/* info sobre reproducao e original reproduzido - somente partitura e registro sonoro */
			,if v9='c' and p(v533) then
				,' ',(v533+|. |),
			,fi,
		,fi,
		/* dados de colecao */
		,if v6:'c' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v23,v24,v25,v27)<>'' then
				,' (',
			,fi, 
			/* autor pessoal nivel colecao */
			,if p(v23) then
				,(|; |+v23^*,| (|v23^r|)|,if nocc(v23)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v23) then ,'.', fi),
			,fi,
			/* autor institucional nivel colecao */
			,if p(v24) then
				,(|; |+v24^*,| (|v24^r|)|,if nocc(v24)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v24) then ,'.', fi),
			,fi,
			/* titulo nivel colecao */
			,if p(v25) then
				,' ',(v25^*+| / |),
			,fi,
			/* numero total de volumes da colecao */
			,if p(v27) then
				,', ',v27/
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
 			,if s(v23,v24,v25,v27)<>'' then
				,').',
			,fi, 
		,fi
		/* dados de serie */
		,if v6:'s' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v30,v31,v32,v35)<>'' then
				,' (',
			,fi, 
			/* titulo da serie */
			,if p(v30) then
				,v30,
			,fi,
			/* volume */
			,if p(v31) then
				,|, v.|v31,
			,fi,
			/* numero */
			,if p(v32) then
				,|, n.|v32,
			,fi,
			/* ISSN */
			,if p(v35) then
				,|, ISSN |v35,
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
			,if s(v30,v31,v32,v35)<>'' then
				,').',
			,fi, 
 		,fi,
		/* Projeto */
		,if v5:'P' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v59,v58,v60)<>'' then
				,' (',
			,fi,
			/* nome do projeto */
			,if p(v59) then
				,v59,
			,fi,
			/* numero */
			,if p(v60) then
				,|, |v60,
			,fi,
			/* instituicao patrocin. do projeto */
			,if p(v58) then
				,|, |v58,
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
			,if s(v59,v58,v60)<>'' then
				,').',
			,fi,
		,fi,
		/* ISBN */
		,if p(v69) then
			,| ISBN |v69|.|,
		,fi,
		/* Acesso eletronico */
		,if p(v8) then
			,if v8^*='Internet' then
				,| Disponível em: &lt;|v8^i|&gt;.|,
				,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
			,else
				,if p(v8^u) then
					,| Disponível em: &lt;|v8^u|&gt;.|,
					,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
				,fi,
			,fi,
		,fi,
	,fi,
	/* Publicacao seriada */
	,if v5*0.1='S' then
		/* autor pessoal nivel analitico */
		,if p(v10) then
			,(|; |+v10^*,| (|v10^r|)|,if nocc(v10)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v10) then ,'.', fi),
		,fi,
		/* autor institucional nivel analitico */
		,if p(v11) then
			,(|; |+v11^*,if nocc(v11)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v11) then ,'.', fi),
		,fi,
		/* titulo nivel analitico */
		,if p(v12) then
			,' ',(v12^*+| / |),"."d12,
		,fi,
		/* dados de evento */
		,if v5:'C' then
			/* titulo do evento */
			,if p(v53) then
				,v53,
			,fi,
			/* data normalizada */
			,if p(v55) then
				,|, |v55*0.4,
			,fi,
			/* cidade */
			,if p(v56) then
				,|, |v56,
			,fi,
			/* pais */
			/* ,if p(v57) then
				,|, |v57,
			,fi, */
			/* instituicao patrocinadora */
			,if p(v52) then
				,|, |v57,
			,fi,
			,'.',
		,fi,
		/* titulo revista */
		,if p(v30) then
			,| <b>|v30|</b>|,
		,fi,
		/* localizacao - cidade, pais */
		,if p(v66) and v66<>'s.l' then
			,|, |v66,
		,fi,
		/* volume */
		,if p(v31) then
			,|, v. |v31,
		,fi,
		/* numero */
		,if p(v32) then
			,|, n. |v32,
		,fi,
		/* nr. paginas inicial e final */
		,if p(v14) then
			,|, p. |v14^f,|-|v14^l,
		,fi,
		/* mes */
		,if p(v65) and v65*4.2<>'00' then
			,', ',@mes.pft,
		,fi,
		/* ano */
		,if p(v65) then
			,| |v65*0.4|.|,
		,else
			,'.',
		,fi,
		/* Projeto */
		,if v5:'P' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v59,v58,v60)<>'' then
				,' (',
			,fi,
			/* nome do projeto */
			,if p(v59) then
				,v59,
			,fi,
			/* numero */
			,if p(v60) then
				,|, |v60,
			,fi,
			/* instituicao patrocin. do projeto */
			,if p(v58) then
				,|, |v58,
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
			,if s(v59,v58,v60)<>'' then
				,').',
			,fi,
		,fi,
		/* ISSN */
		,if p(v35) then
			,| ISSN |v35|.|,
		,fi,
		/* Acesso eletronico */
		,if p(v8) then
			,if v8^*='Internet' then
				,| Disponível em: &lt;|v8^i|&gt;.|,
				,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
			,else
				,if p(v8^u) then
					,| Disponível em: &lt;|v8^u|&gt;.|,
					,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
				,fi,
			,fi,
		,fi,
	,fi,
	/* Teses */
	,if v5='T' then
		/* nivel analitico */
		,if v6*0.1='a' then
			/* autor pessoal nivel analitico */
			,if p(v10) then
				,(|; |+v10^*,| (|v10^r|)|,if nocc(v10)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v10) then ,'.', fi),
			,fi,
			/* autor institucional nivel analitico */
			,if p(v11) then
				,(|; |+v11^*,if nocc(v11)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v11) then ,'.', fi),
			,fi,
			/* titulo nivel analitico */
			,if p(v12) then
				,' ',(v12^*+| / |),". In:"d12,
			,fi,
		,fi,
		/* nivel monografico */
		,if v6*0.1='m' or v6*0.2='am' then
			/* autor pessoal nivel monografico */
			,if p(v16) then
				,(|; |+v16^*,| (|v16^r|)|,if nocc(v16)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v16) then ,'.', fi),
			,fi,
			/* autor institucional nivel monografico */
			,if p(v17) then
				,(|; |+v17^*,if nocc(v17)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v17) then ,'.', fi),
			,fi,
			/* titulo nivel monografico */
			,if p(v18) then
				," <b>"d18,(v18^*+| / |),"</b>."d18,
			,fi,
			/* ano de deposito */
			/* desabilitado pois nao tem tag */
			/* ,if p(v???) then
				,v???,'. ',
			,fi, */
			/* nr. paginas */
			/* somente se nao for analitica */
			,if p(v20) and v6*0.1<>'a' then
				,| |v20,
				,if not v20:'v' and not v20:'p' then
					,' f.',
				,fi,
			,fi,
			/* nr. paginas inicial e final */
			,if p(v14) then
				,| p. |v14^f,|-|v14^l|.|,
			,fi,
			/* grau de dissertacao/tese */
			,if p(v51) then
				,' Dissertação para o grau de ',v51,
			,fi,
			/* Unidade da defesa */
			,if p(v50) then
				,', ',v50,
			,fi,
			/* localizacao - cidade, pais */
			,if p(v66) and v66<>'s.l' then
				,if p(v50) or p(v51) then
					,', ',
				,fi,
				,if p(v67) then
					,v66,|, |v67|,|,
				,else
					,v66|,|,
				,fi,
			,fi,
			/* ano de defesa */
			/* nao tem tag */
			/* considerado ano de publicacao */
			,if p(v64) then
				,| |v64,'. ',
			,fi,
		,fi,
		/* dados de serie */
		,if v6:'s' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v30,v31,v32,v35)<>'' then
				,' (',
			,fi, 
			/* titulo da serie */
			,if p(v30) then
				,v30,
			,fi,
			/* volume */
			,if p(v31) then
				,|, v.|v31,
			,fi,
			/* numero */
			,if p(v32) then
				,|, n.|v32,
			,fi,
			/* ISSN */
			,if p(v35) then
				,|, ISSN |v35,
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
			,if s(v30,v31,v32,v35)<>'' then
				,').',
			,fi, 
 		,fi,
		/* Acesso eletronico */
		,if p(v8) then
			,if v8^*='Internet' then
				,| Disponível em: &lt;|v8^i|&gt;.|,
				,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
			,else
				,if p(v8^u) then
					,| Disponível em: &lt;|v8^u|&gt;.|,
					,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
				,fi,
			,fi,
		,fi,
	,fi,
,fi,

/* registro sonoro */ 
,if v9='j' or v9='i' then
	,if v5*0.1='N' or v5*0.1='M' then
		/* nivel analitico */
		,if v6*0.1='a' then
			/* autor pessoal nivel analitico */
			,if p(v10) then
				,(|; |+v10^*,| (|v10^r|)|,if nocc(v10)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v10) then ,'.', fi),
			,fi,
			/* autor institucional nivel analitico */
			,if p(v11) then
				,(|; |+v11^*,if nocc(v11)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v11) then ,'.', fi),
			,fi,
			/* titulo nivel analitico */
			,if p(v12) then
				,' ',(v12^*+| / |),". In: "d12,
			,fi,
		,fi,
		/* nivel monografico */
		,if v6*0.1='m' or v6*0.2='am' then
			/* autor pessoal nivel monografico */
			,if p(v16) then
				,(|; |+v16^*,| (|v16^r|)|,if nocc(v16)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v16) then ,'.', fi),
			,fi,
			/* autor institucional nivel monografico */
			,if p(v17) then
				,(|; |+v17^*,if nocc(v17)>3 and iocc=3 then ,' et al. ',break, fi,if iocc=nocc(v17) then ,'.', fi),
			,fi,
			/* titulo nivel monografico */
			,if p(v18) then
				," <b>"d18,(v18^*+| / |),"</b>."d18,
			,fi,
			/* indic. volume nivel monografico */
			,if p(v21) then
				,| |v21|.|,
			,fi,
			/* localizacao - cidade, pais */
			,if p(v66) and v66<>'s.l' then
				,if p(v67) then
					,| |v66,|, |v67|:|,
				,else
					,| |v66|:|,
				,fi,
			,fi,
			/* editora */
			,if p(v62) and v62<>'s.n' then
				,if v9<>'i' and v9<>'j' then
					,' ',(v62+|; |),
				,else
					,' ',(v62^f+|; |),
				,fi,
				,if p(v64) then ,',', else ,'.', fi,
			,fi,
			/* data de publicacao */
			,if a(v64) then
				,' s.d.',
			,else
				,| |v64,'.',
			,fi,
			/* informacao descritiva */
			/* se for no formato novo */
			,(if s(v38^a,v38^b,v38^c)<>'' then
				,| |v38^a,
				,if p(v38^b) and p(v38^a) then ,',', fi,
				,| |v38^b,
				,if p(v38^c) and s(v38^a,v38^b)<>'' then ,',', fi,
				,| |v38^c,
				,'.',
			,fi),
			/* se for no formato antigo */
			,if s(v38^a,v38^b,v38^c)='' and s(v38^*)<>'' then
				,' ',(v38+|, |),'.',
			,fi,
			/* nota de conteudo formatada - somente partitura e registro sonoro */
			,if p(v505) then
				,' ',(v505+|. |),
			,fi,
			/* info sobre reproducao e original reproduzido - somente partitura e registro sonoro */
			,if p(v533) then
				,' ',(v533+|. |),
			,fi,
			/* tipo de registro musical */
			,if p(v117) then
				,' ',v117|. |,
			,fi,
		,fi,
		/* dados de colecao */
		,if v6:'c' then
			/* se houver dados para apresentar, abrir parentesis */
			,if s(v25,v27)<>'' then
				,' (',
			,fi, 
			/* titulo nivel colecao */
			,if p(v25) then
				,' ',(v25^*+| / |),
			,fi,
			/* numero total de volumes da colecao */
			,if p(v27) then
				,', ',v27/
			,fi,
			/* se foram apresentados dados, finalizar com parentesis */
 			,if s(v25,v27)<>'' then
				,').',
			,fi, 
		,fi
		/* Acesso eletronico */
		,if p(v8) then
			,if v8^*='Internet' then
				,| Disponível em: &lt;|v8^i|&gt;.|,
				,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
			,else
				,if p(v8^u) then
					,| Disponível em: &lt;|v8^u|&gt;.|,
					,| Acesso em: |v91*6.2,|/|v91*4.2,|/|v91*0.4|.|,
				,fi,
			,fi,
		,fi,
	,fi,
,fi,

,'</p>'/,
/* fim do formato generico */
        '</font></td></tr>'
   '</table>',

'<p><br>',
